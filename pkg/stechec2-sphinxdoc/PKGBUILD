# Maintainer: Prologin <serveur@prologin.org>

_pkgver=prologin2018
pkgver=r641
pkgname=stechec2-sphinxdoc
pkgrel=1
pkgdesc="Stechec2 sphinxdoc for ${pkgver}"
arch=(any)
makedepends=(python-sphinx stechec2)
url="http://prologin.org/"
license=('GPL')

pkgver() {
    echo r$(pacman -Qi stechec2 | grep Version | cut -dr -f3 | cut -d- -f1)
}

build() {
    # Empty $srcdir
    rm -rf "$srcdir"
    mkdir "$srcdir"

    cd "$srcdir"
    stechec2-generator sphinxdoc "${_pkgver}" .
    sphinx-quickstart --quiet --project "API Prologin" --author "Prologin" \
      -v 2018 --release 1 --language fr --master index --suffix .rst \
      --makefile --no-batchfile .

    cat > index.rst <<EOF
Bievenue sur la documentation de l'API !
========================================

.. toctree::
   :maxdepth: 2

   file
EOF
    make html
}

package() {
    mkdir -p "$pkgdir/usr/share/doc/stechec2/${_pkgver}"
    cp -r "$srcdir/_build/html" "$pkgdir/usr/share/doc/stechec2/${_pkgver}"
}
