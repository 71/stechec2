# Maintainer: Prologin <serveur@prologin.org>

_url=git@github.com/prologin
_pkgver=prologin2017
pkgname=stechec2
pkgrel=1
pkgver=r654
pkgdesc="Stechec2 with ${pkgver} rules"
arch=('i686' 'x86_64')
url="http://prologin.org/"
license=('GPL')
provides=('stechec2' $pkgver)
depends=('gflags' 'zeromq' 'ocaml' 'python-yaml' 'ruby')
makedepends=('git')
source=("git+ssh://${_url}/stechec2"
        "git+ssh://${_url}/${_pkgver}")
sha512sums=(SKIP
            SKIP)

pkgver() {
    cd "$pkgname"
    stechec2_commits=$(git rev-list --count HEAD)
    cd "../$_pkgver"
    prologin_commits=$(git rev-list --count HEAD)
    echo r$((stechec2_commits + prologin_commits))
}

prepare() {
    cd stechec2
    ln --force --symbolic "${srcdir}/${_pkgver}" games/
}

build() {
    cd stechec2
    ./waf.py configure build --prefix=/usr --libdir=/usr/lib \
                             --with-games=${_pkgver}
}

package() {
    cd stechec2
    ./waf.py install --destdir="$pkgdir"
}
